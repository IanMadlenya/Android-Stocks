// Main build script for Stocks
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build release APK
//        gradlew test                    Run local unit test
//        gradlew connectedAndroidTest    Run instrumented unit test


final VERSION_MAJOR = 1 // max two digits
final VERSION_MINOR = 0 // max two digits
final VERSION_PATCH = 0 // max two digits
final VERSION_BUILD = 0 // max three digits


apply plugin: "com.android.application"
apply plugin: "me.tatarka.retrolambda"
apply from: "../utils.gradle"


android {
	compileSdkVersion 26
	buildToolsVersion "26.0.1"

	defaultConfig {
		applicationId "com.example.stocks"
		minSdkVersion 16
		targetSdkVersion 26
		versionCode getVersionCode(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH, VERSION_BUILD)
		versionName getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		release {
			// passwords and alias are obtained via askForPasswords task
			storeFile file("../${project.property('keystore.file')}")
			storePassword ""
			keyAlias ""
			keyPassword ""
		}
	}

	buildTypes {
		debug {
			buildConfigField "boolean", "LOGS", "true"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "true"

			versionNameSuffix "-debug"
		}

		release {
			buildConfigField "boolean", "LOGS", "false"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "false"

			signingConfig signingConfigs.release
			zipAlignEnabled true
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	dataBinding {
		enabled = true
	}

	applicationVariants.all {
		variant ->
			if(variant.buildType.name.equals("release")) {
				renameArtifact("stocks", variant)
			}
	}
}


dependencies {
	compile fileTree(dir: "libs", include: ["*.jar"])
	compile "android.arch.lifecycle:runtime:1.0.0-alpha9"
	compile "android.arch.lifecycle:extensions:1.0.0-alpha9"
	compile "com.android.support:support-compat:26.0.2"
	compile "com.android.support:support-core-utils:26.0.2"
	compile "com.android.support:support-core-ui:26.0.2"
	compile "com.android.support:support-fragment:26.0.2"
	compile "com.android.support:appcompat-v7:26.0.2"
	compile "com.android.support:recyclerview-v7:26.0.2"
	compile "com.android.support:support-annotations:26.0.2"
	compile "com.android.support:design:26.0.2"
	compile "com.android.support.constraint:constraint-layout:1.0.2"
	compile "com.github.bumptech.glide:glide:3.8.0"
	compile "com.github.bumptech.glide:okhttp3-integration:1.5.0@aar"
	compile "com.google.code.gson:gson:2.8.1"
	compile "com.squareup.okhttp3:okhttp:3.8.1"
	compile "com.squareup.okhttp3:logging-interceptor:3.8.1"
	compile "com.squareup.retrofit2:retrofit:2.3.0"
	compile "com.squareup.retrofit2:adapter-rxjava2:2.3.0"
	compile "com.squareup.retrofit2:converter-gson:2.3.0"
	compile "eu.inloop:androidviewmodel:1.3.3"
	compile "io.reactivex.rxjava2:rxandroid:2.0.1"
	compile "io.reactivex.rxjava2:rxjava:2.1.3"
	compile "org.alfonz:alfonz-adapter:0.7.0"
	compile "org.alfonz:alfonz-graphics:0.7.0"
	compile "org.alfonz:alfonz-mvvm:0.7.0"
	compile "org.alfonz:alfonz-rest:0.7.0"
	compile "org.alfonz:alfonz-rx:0.7.0"
	compile "org.alfonz:alfonz-utility:0.7.0"
	compile "org.alfonz:alfonz-view:0.7.0"
	debugCompile "com.squareup.leakcanary:leakcanary-android:1.5.1"
	releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.5.1"
	testCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.5.1"
	testCompile "junit:junit:4.12"
	testCompile "org.mockito:mockito-core:2.9.0"
	annotationProcessor "android.arch.lifecycle:compiler:1.0.0-alpha9"
}
